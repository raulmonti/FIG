// Tests for conditional (re)sampling of clocks
// Budde, Monti, D'Argenio | 2021
//
// Queue with exponential arrivals, and service times ~ gamma(pi+1,0.12345678)
//
// Sample command line:
//
// $ fig --parlare 1 --stop-time 30s --rng-seed 387897885 --amono -e restart --thresholds-adhoc "6:2,7:2,8:3,9:3,10:2,11:3,12:3,13:3" gamma.sa
//
// Compare against flag "--no-resampling"


module Arrivals
	clkArr: clock;
	[Arr!] @ clkArr -> (clkArr'= exponential(1.0));
endmodule

const float k = 4.14159265 ;  // shape of the gamma
const float t = 0.12345678 ;  // scale of the gamma
const int C = 19;

module Queue
	clkSer: clock;
	q: [0..C];
	// Packet arrival
	[Arr?] q == 0          -> (q'= q+1) & (clkSer'= gamma(k,t));
	[Arr?] q  > 0 & q < C  -> (q'= q+1);
	[Arr?] q == C          -> ;
	// Packet service
	[Ser!] q == 1 @ clkSer -> (q'= q-1);
	[Ser!] q  > 1 @ clkSer -> (q'= q-1) & (clkSer'= gamma(k,t));
endmodule

properties
	S[ 60:6060 ]( q >= C )
endproperties
