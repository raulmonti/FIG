// Tests for conditional (re)sampling of clocks
// Budde, Monti, D'Argenio | 2021
//
// Queue with exponential arrivals, and service times ~ lognormal(-1.77,0.777)
//
// Sample command line:
//
// $ fig --parlare 1 --stop-time 30s --rng-seed 897187207 --amono -e restart --thresholds-adhoc "4:2,5:3,6:3,7:3,8:2,9:2,10:3,11:2,12:3,13:3" lognormal.sa
//
// Compare against flag "--no-resampling"


module Arrivals
	clkArr: clock;
	[Arr!] @ clkArr -> (clkArr'= exponential(1.0));
endmodule

const float m = -1.77;  // mean of the (inherent) Gaussian
const float s = 0.777;  // standard deviation of the (inherent) Gaussian
const int C = 19;

module Queue
	clkSer: clock;
	q: [0..C];
	// Packet arrival
	[Arr?] q == 0          -> (q'= q+1) & (clkSer'= lognormal(m,s));
	[Arr?] q  > 0 & q < C  -> (q'= q+1);
	[Arr?] q == C          -> ;
	// Packet service
	[Ser!] q == 1 @ clkSer -> (q'= q-1);
	[Ser!] q  > 1 @ clkSer -> (q'= q-1) & (clkSer'= lognormal(m,s));
endmodule

properties
	S[ 60:6060 ]( q >= C )
endproperties
