// Tests for conditional (re)sampling of clocks
// Budde, Monti, D'Argenio | 2021
//
// Queue with exponential arrivals, and service times ~ uniform(a,b)
//
// Sample command line:
//
// $ fig --parlare 1 --stop-time 15s --amono -e restart --thresholds-adhoc "9:3,10:3,11:2,12:3,13:3,14:3,15:2,16:3,17:3,18:3" uniform.sa
//
// Compare against flag "--no-resampling"
//
// Alternative model:
// make arrivals ~ uniform(0.222,1.11) and use thresholds:
// "5:2,7:2,8:2,9:3,10:2,11:2,12:2,13:2,14:3,15:2,16:2,17:2,18:2"


module Arrivals
	clkArr: clock;
	[Arr!] @ clkArr -> (clkArr'= exponential(1.0));
endmodule

const float a = 0.50;  // lower bound uniform
const float b = 0.75;  // upper bound uniform
const int C = 19;

module Queue
	clkSer: clock;
	q: [0..C+2];
	// Packet arrival
	[Arr?] q == 0          -> (q'= q+1) & (clkSer'= uniform(a,b));
	[Arr?] q  > 0 & q < C  -> (q'= q+1);
	[Arr?] q == C          -> ;
	// Packet service
	[Ser!] q == 1 @ clkSer -> (q'= q-1);
	[Ser!] q  > 1 @ clkSer -> (q'= q-1) & (clkSer'= uniform(a,b));
endmodule

properties
	S[ 60:6060 ]( q >= C )
endproperties
