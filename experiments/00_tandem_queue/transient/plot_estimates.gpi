#!/usr/bin/env gnuplot
#
# Author:  Carlos E. Budde
# Date:    17.07.2016
# License: GPLv3
#
#   This script expects some variables to be defined on invocation.
#   Sample invocation line:
#   $ gnuplot -e "SPLIT=2;AH='AH_file.dat';..." plot_estimates.gpi
#

# Check external variables were defined
if (!exists("XTIC"))  exit error "[ERROR] Missing 'XTIC' variable"
if (!exists("SPLIT")) exit error "[ERROR] Missing 'SPLIT' variable"
# The following var names should match those in "IFUNS" from main.sh
## if (!exists("MC")) exit error "[ERROR] Missing 'MC' variable"
if (!exists("AH")) exit error "[ERROR] Missing 'AH' variable"
if (!exists("AC")) exit error "[ERROR] Missing 'AC' variable"
if (!exists("AM")) exit error "[ERROR] Missing 'AM' variable"

# Output file setup
set encoding utf8
set term pdf monochrome dashed enhanced size 7,6 font 'Verdana,15'
fout="plot_estimates_s".SPLIT.".pdf"
set out fout

# General plot options
set title offset 0,0 "{/Times*1.8 Transient analysis for the tandem queue}\n\nSplitting: ".SPLIT
set xlab offset 0,-.1 "{/*1.2 Queues capacity}"
set ylab offset -.1,0 "{/*1.2 Overflow probability}"
set border 1+2+4+8  # bottom + left + top + right borders
set grid
#set rmargin 5
#set bmargin 5
set log y
set format y "%.1e"
set xtics XTIC font ",14"
set ytics      font ",14"
set auto fix
set offsets 0.3,0.3,0.3,0.1

# Plot sequence
set key top right font ",16"
set bars 0.4  # size of the bars at the end of the error ranges
set style fill transparent solid 0.1 noborder
## plot MC u 0:($2-$3/2):($2+$3/2):xtic(1) w filledcurves fc 0 t '',      \
##      MC u 0:2:($3/2):xtic(1) w errorbars pt 7 ps .4 lt 1 t 'nosplit',  \

plot AH u 0:($2-$3/2):($2+$3/2):xtic(1) w filledcurves fc 0 t '',      \
     AH u 0:2:($3/2):xtic(1) w errorbars pt 10 lt 1 t 'ad hoc',        \
     AC u 0:($2-$3/2):($2+$3/2):xtic(1) w filledcurves fc 0 t '',      \
     AC u 0:2:($3/2):xtic(1) w errorbars pt  6 lt 1 t 'compositional', \
     AM u 0:($2-$3/2):($2+$3/2):xtic(1) w filledcurves fc 0 t '',      \
     AM u 0:2:($3/2):xtic(1) w errorbars pt  4 lt 1 t 'monolithic'

## Report success and exit
#print "Estimates plotted in file ".fout
#exit
