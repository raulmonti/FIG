#!/usr/bin/env gnuplot
#
# Author:  Carlos E. Budde
# Date:    17.07.2016
# License: GPLv3
#
#   This script expects some variables to be defined on invocation.
#   Sample invocation line:
#   $ gnuplot -e "SPLIT=2;AH='AH_file.dat';..." plot_estimates.gpi
#

# Check external variables were defined
#die(n) = system("killall gnuplot &>/dev/null")  # call like: a=die(1)
if (!exists("XMIN"))  exit error "[ERROR] Missing 'XMIN' variable"
if (!exists("XMAX"))  exit error "[ERROR] Missing 'XMAX' variable"
if (!exists("XSTEP")) exit error "[ERROR] Missing 'XSTEP' variable"
if (!exists("SPLIT")) exit error "[ERROR] Missing 'SPLIT' variable"
# The following var names should match those in "IFUNS" from main.sh
if (!exists("MC")) exit error "[ERROR] Missing 'MC' variable"
if (!exists("AH")) exit error "[ERROR] Missing 'AH' variable"
if (!exists("AC")) exit error "[ERROR] Missing 'AC' variable"
if (!exists("AM")) exit error "[ERROR] Missing 'AM' variable"

# Output file setup
set encoding utf8
fout="tandem_queue_estimates_s".SPLIT.".pdf"
set out fout
set term pdf monochrome dashed size 7,6 font 'Verdana,15'

# General plot options
set title offset 0,0 "{/Times*1.8 Transient analysis for the tandem queue}\n\nSplitting: ".SPLIT
set xlab offset 0,-.1 "{/*1.2 Queues capacity}"
set ylab offset -.1,0 "{/*1.2 Overflow probability}"
set border 1+2+4+8  # bottom + left + top + right borders
set grid
#set rmargin 5
#set bmargin 5
set log y
set format y "%.1e"
set xrange [XMIN:XMAX]
set xtics XSTEP font ",14"
set ytics       font ",14"
set auto fix
set offsets 0.3,0.3,0.3,0.1

# Plot sequence
set key top right font ",16"
set style fill transparent solid 0.25 noborder
plot AH u ($1):($2-$3/2):($2+$3/2) w filledcurves fc 0 t '', \
	 '' u ($1):2 w lp lt 2 lc 0 pt 9 ps 1 lw 2 t 'ad hoc',        \
	 AM u ($1):($2-$3/2):($2+$3/2) w filledcurves fc 0 t '', \
	 '' u ($1):2 w lp lt 1 lc 0 pt 7 ps 1 lw 2 t 'monolithic'

# Report success and exit
print "Estimates plotted in file ".fout
exit
